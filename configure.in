dnl Process this file with autoconf to produce a configure script.
dnl ---
AC_REVISION($Revision: 1.16 $)dnl
dnl	$Log: configure.in,v $
dnl	Revision 1.16  2003/12/17 19:05:02  gronej
dnl	SNACC baseline merged with PER v1_7 tag
dnl	
dnl	Revision 1.15.2.1  2003/11/05 14:58:53  gronej
dnl	working PER code merged with esnacc_1_6
dnl	
dnl	Revision 1.15  2003/03/20 15:50:42  mcphersc
dnl	fixed bug in checking for threads, added bracket around variables
dnl	
dnl	Revision 1.14  2003/03/12 13:43:57  mcphersc
dnl	took out debug code
dnl	
dnl	Revision 1.13  2003/03/11 19:17:46  mcphersc
dnl	added no threads
dnl	
dnl	Revision 1.12  2003/03/11 16:41:15  mcphersc
dnl	Added check for threads
dnl	
dnl	Revision 1.11  2003/02/11 15:40:26  mcphersc
dnl	added ignorestringcheck
dnl	
dnl	Revision 1.10  2003/02/04 13:05:25  mcphersc
dnl	 commit config
dnl	
dnl	Revision 1.9  2003/01/15 16:10:55  mcphersc
dnl	Added shared lib check
dnl	
dnl	Revision 1.8  2002/12/17 19:05:23  mcphersc
dnl	*** empty log message ***
dnl	
dnl	Revision 1.7  2002/12/13 18:30:42  mcphersc
dnl	Fixed SNACC_BUILDDIR
dnl	
dnl	Revision 1.6  2002/12/13 17:24:22  mcphersc
dnl	added version check for gcc
dnl	
dnl	Revision 1.5  2002/12/13 14:46:15  mcphersc
dnl	Added AC_ARG_ENABLE for tcl and meta
dnl	
dnl	Revision 1.4  2002/12/10 13:29:01  mcphersc
dnl	Added AC_ARG_ENABLE so that --enable-tcl could be added
dnl	
dnl	Revision 1.3  2002/09/16 15:55:51  mcphersc
dnl	took out some un-needed checks
dnl	
dnl	Revision 1.2  2002/08/23 12:56:50  mcphersc
dnl	Added the --enable-debug macro the configure.in
dnl	
dnl	Revision 1.1.1.1  2000/08/21 20:35:45  leonberp
dnl	First CVS Version of SNACC.
dnl	
dnl	Revision 1.13  1997/09/04 13:54:04  wan
dnl	A little more portability
dnl
dnl	Revision 1.12  1997/02/28 13:39:35  wan
dnl	Modifications collected for new version 1.3: Bug fixes, tk4.2.
dnl
dnl	Revision 1.11  1997/02/16 16:50:27  rj
dnl	made return *this after calling abort()'' a compile time option.
dnl
dnl	Revision 1.10  1997/02/15 20:06:27  rj
dnl	adjust to changed AC_TRY_COMPILE macro
dnl
dnl	Revision 1.9  1997/02/15 20:01:38  rj
dnl	check whether the compiler supports volatile functions (and whether abort() is volatile).
dnl
dnl	Revision 1.8  1997/01/01 19:57:01  rj
dnl	changes for autoconf version 2.12
dnl
dnl	Revision 1.7  1995/09/07  18:36:47  rj
dnl	psbook and psnup are looked for (used by .../doc/makefile)
dnl
dnl	Revision 1.6  1995/07/24  14:44:47  rj
dnl	don't use gcc/g++ with -pipe, compiling some files may exceed virtual memory.
dnl
dnl	look for tclsh(1). tcl-lib uses it to construct the tclIndex file. don't look for Tcl/Tk if the tclsh is absent.
dnl
dnl	look for patch(1). the c-lib uses it to patch tbl.h.
dnl
dnl	search for tree-3.6's libtktree.a and set TREELIBS in .../makehead accordingly.
dnl
dnl	check for memset(3), memcpy(3) and memcmp(3). .../snacc.h reverts to bzero(3), bcopy(3) and bcmp(3) if necessary.
dnl
dnl	Revision 1.5  1995/02/20  11:18:41  rj
dnl	cpp switch HAVE_VARIABLE_SIZED_AUTOMATIC_ARRAYS added.
dnl	check for isinf(3) and finite(3) added.
dnl
dnl	Revision 1.4  1995/02/17  15:15:44  rj
dnl	hack to let makedepend find .h files the way gcc does.
dnl
dnl	Revision 1.3  1995/02/17  14:26:40  rj
dnl	adjustments for autoconf 2.x
dnl
dnl	Revision 1.2  1994/10/08  04:29:37  rj
dnl	search for Tcl/Tk
dnl
dnl	Revision 1.1  1994/09/01  00:51:22  rj
dnl	first check-in (new file).
dnl

dnl
dnl Disable the cache
define([AC_CACHE_LOAD],)
define([AC_CACHE_SAVE],)
AC_INIT( compiler/core/snacc.c)
AC_CONFIG_HEADER( config.h)

SNACC_BUILDDIR=`pwd`
AC_SUBST(SNACC_BUILDDIR)


dnl
dnl Default compiler flags include the debug option, snacc defaults to no debug
CFLAGS=
CXXFLAGS=
AC_SUBST(CFLAGS)
AC_SUBST(CXXFLAGS)
AC_ARG_ENABLE(debug,[--enable-debug [default no]],mydebug=1,)
AC_SUBST(mydebug)

dnl
dnl If  tcl is enabled then set the defines
AC_ARG_ENABLE(tcl,[--enable-tcl [default yes]],
[echo Enable val is $enableval; if test $enableval = yes; then
	CFLAGS="-DENABLE_TCL -DENABLE_META"
	CXXFLAGS="-DENABLE_TCL -DENABLE_META"
	AC_SUBST(CFLAGS)
	AC_SUBST(CXXFLAGS)
fi
])dnl

dnl
dnl If  tcl is enabled then set the defines
AC_ARG_ENABLE(meta,[--enable-meta [default yes]],
[echo Enable val is $enableval; if test $enableval = yes; then
	CFLAGS="-DENABLE_META"
	CXXFLAGS="-DENABLE_META"
	AC_SUBST(CFLAGS)
	AC_SUBST(CXXFLAGS)
fi
])dnl

dnl
dnl SPECIAL SWITCH DO NOT USE IT WILL BURN YOUR SYSTEM UP
dnl
AC_ARG_ENABLE(ignorestringcheck, [--enable-ignorestringcheck [default no]],
[echo Enable val is $enableval; if test $enableval = yes; then
	CFLAGS="-DIGNORESTRINGCHECK"
	CXXFLAGS="-DIGNORESTRINGCHECK"
	AC_SUBST(CFLAGS)
	AC_SUBST(CXXFLAGS)
fi
])dnl
	
dnl --- alternative programs:
AC_PROG_MAKE_SET
AC_PROG_CXX_NO_G
AC_PROG_CC_NO_G
AC_PROG_CC
AC_PROG_CXX
AC_ISC_POSIX
if test -n "$GCC"; then
dnl   saveCC="$CC"
dnl   CC="$CC -pipe"
dnl   AC_MSG_CHECKING( whether $saveCC takes -pipe)
dnl   AC_TRY_LINK( , , [AC_MSG_RESULT( yes)], [AC_MSG_RESULT( no); CC="$saveCC"])

  # hack to let X11 makedepend find .h file a little more the way gcc does:
  AC_MSG_CHECKING( for $CC's include path)
  gcclibdir=`$CC -v 2>&1 | sed -n -e 's:Reading specs from \(.*\)/specs:\1:p'`
  gxxincdir=`which gcc | sed "s/bin\/gcc//"`
  MKDEP_CCINC="-I$gxxincdir/lib/g++-include -I/usr/local/include -I$gcclibdir/include"
  AC_MSG_RESULT( $MKDEP_CCINC)
  AC_SUBST( MKDEP_CCINC)
fi
AC_PROG_GCC_TRADITIONAL
AC_PROG_CC_C_O
if eval "test \"`echo '$ac_cv_prog_cc_'${ac_cc}_c_o`\" = no"; then
  AC_MSG_WARN( since -c and -o don't seem to work together, the makefiles probably won't work!)
fi
AC_MSG_CHECKING( whether ANSI or K&R style C)
AC_TRY_RUN( [main (int argc, char **argv) { void *p; return 0; }], [AC_MSG_RESULT( ANSI); AC_DEFINE( __USE_ANSI_C__)], AC_MSG_RESULT( K&R), AC_MSG_RESULT( K&R))
AC_CHECK_GCC_VERSION
AC_LANG_CPLUSPLUS
dnl if test -n "$GCC"; then
dnl   saveCXX="$CXX"
dnl   CXX="$CXX -pipe"
dnl   AC_MSG_CHECKING( whether $saveCXX takes -pipe)
dnl   AC_TRY_LINK( , , [AC_MSG_RESULT( yes)], [AC_MSG_RESULT( no);CC="$saveCC"])
dnl fi
AC_MSG_CHECKING( for bool built-in)
AC_TRY_COMPILE( , bool b = false;, [AC_DEFINE( BOOL_BUILTIN) AC_MSG_RESULT( yes)], AC_MSG_RESULT( no))
AC_MSG_CHECKING( for variable sized automatic arrays)
AC_TRY_COMPILE( , changequote(<,>)int i = 42; char a[++i]; *a = i;changequote([,]), [AC_DEFINE( HAVE_VARIABLE_SIZED_AUTOMATIC_ARRAYS) AC_MSG_RESULT( yes)], AC_MSG_RESULT( no))
AC_MSG_CHECKING( for volatile functions)
AC_TRY_COMPILE( , changequote(<,>)abort();changequote([,]), [AC_MSG_RESULT( yes)], [AC_DEFINE( COMPILER_WITHOUT_VOLATILE_FUNCTIONS) SNACC_NOVOLAT=-novolat; AC_SUBST( SNACC_NOVOLAT) AC_MSG_RESULT( no)])
AC_LANG_C
AC_DECL_YYTEXT
AC_GET_LD_FLAG
AC_SUBST(LFLAGS)
dnl AC_PROG_LEX
case "$LEX" in
  lex)
    LFLAGS="-Nc10000"
  ;;
  flex)
    CFLAGS="$CFLAGS -DFLEX_IN_USE -DYYTEXT_POINTER"
esac
AC_SUBST( LFLAGS)
AC_SUBST( CFLAGS)
# the boot strapping code in .../c-lib/ needs patch:
AC_CHECK_PROGS( PATCH, patch, false)
AC_PROG_YACC
AC_CHECK_PROGS( MKDEP, makedepend mkdep mkdep.sh, $CC)
AC_CHECK_PROGS( AR, ar, ar)
AC_PROG_RANLIB
AC_PROG_INSTALL
dnl --- header files:
AC_HEADER_TIME
AC_HEADER_DIRENT
AC_TYPE_SIZE_T
AC_TYPE_UID_T
AC_HEADER_STDC
AC_CHECK_HEADERS( unistd.h memory.h string.h malloc.h fcntl.h)
dnl Check for threads
HAVE_PTHREAD=yes
AC_CHECK_LIB(pthread,pthread_create,[HAVE_PTHREAD=yes],[HAVE_PTHREAD=no],-lc)
AC_SUBST(HAVE_PTHREAD)
dnl

AC_FUNC_VPRINTF
dnl --- system and compiler characteristics:
AC_C_CONST
AC_C_BIGENDIAN
AC_CHECK_SIZEOF( short)
AC_CHECK_SIZEOF( int)
AC_CHECK_SIZEOF( long)
AC_CHECK_SIZEOF( double)
AC_CHECK_LIB( m, sin)
AC_CHECK_FUNCS( isinf finite)
AC_CHECK_FUNCS( memset memcpy memcmp)
dnl ---
AC_PATH_XTRA
AC_CHECK_PROGS( TCLSH, tclsh, false)
if test "$TCLSH" != false; then
  AC_CHECK_HEADER(
    tcl.h,
    [AC_CHECK_LIB( ld, ldopen, TCLOTHERLIBS=-lld)
    AC_CHECK_LIB(
      tcl,
      Tcl_CreateInterp,
      [saveCFLAGS="$CFLAGS"
      CFLAGS="$CFLAGS $X_CFLAGS"
      AC_CHECK_LIB(
	tk,
	Tk_CreateWindow,
	[AC_DEFINE( HAVE_TCL)
	TCLLIBS="-ltk -ltcl $TCLOTHERLIBS"
	AC_LANG_CPLUSPLUS
	AC_CHECK_LIB(
	  TkTree,
	  Tree_Init,
	  [TREELIBS="-lTkTree"],
	  [AC_MSG_RESULT( [tcl/tk libs found, but tree widget is missing])],
	  -lTkTree -ltk -ltcl $TCLOTHERLIBS $X_LIBS $X_PRE_LIBS -lX11 $X_EXTRA_LIBS)
	AC_LANG_C],
	[AC_MSG_RESULT( [tcl lib found, but tk lib is missing])],
	-ltcl $TCLOTHERLIBS $X_LIBS $X_PRE_LIBS -lX11 $X_EXTRA_LIBS)
      CFLAGS="$saveCFLAGS"],
      [AC_MSG_RESULT( [tcl library not found])],
      $TCLOTHERLIBS -lm)])
fi
AC_SUBST( TCLLIBS)
AC_SUBST( TREELIBS)
dnl ---
dnl ---
AC_OUTPUT( makehead, date > stamp-h)
dnl --- finis
